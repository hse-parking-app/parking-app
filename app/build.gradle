plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.8.0'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
}

if (file('secrets.gradle').exists()) {
    apply from: 'secrets.gradle'
} else {
    project.ext.signKeyPassword = System.getenv("SIGNKEY_PASSWORD")
    project.ext.signKeyAlias = System.getenv("SIGNKEY_ALIAS")
}

android {
    namespace 'com.hse.parkingapp'
    compileSdk 33

    defaultConfig {
        applicationId "com.hse.parkingapp"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            storeFile file('parking_app_signkey.jks')
            storePassword project.ext.signKeyPassword
            keyAlias project.ext.signKeyAlias
            keyPassword project.ext.signKeyPassword
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        signedRelease {
            signingConfig signingConfigs.release
        }
    }
    applicationVariants.all {
        variant ->
            variant.outputs.each {
                output ->
                    def name = "${project.name}-${variant.versionName}-${variant.buildType.name}.apk"
                    name = name.replace('app', 'parking-app')
                    output.outputFileName = name
            }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        freeCompilerArgs += ["-Xjvm-default=all"]
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "$compose_compiler_version"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    // Import the Compose BOM (Bill of Materials, contemporary versions of compose libraries)
    implementation platform('androidx.compose:compose-bom:2023.05.01')

    implementation "androidx.core:core-ktx:$core_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_runtime_ktx_version"
    implementation "androidx.activity:activity-compose:$activity_compose_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$viewmodel_version"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$systemuicontroller_version"

    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.navigation:navigation-compose"
    implementation "androidx.compose.material3:material3"
    implementation "androidx.compose.ui:ui-util"

    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$serialization_version"
    implementation "com.auth0.android:jwtdecode:$jwt_version"

    implementation "com.google.dagger:hilt-android:$dagger_hilt_version"
    kapt "com.google.dagger:hilt-compiler:$dagger_hilt_compiler_version"
    kapt "androidx.hilt:hilt-compiler:$hilt_compiler_version"
    implementation "androidx.hilt:hilt-navigation-compose:$hilt_navigation_version"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation "androidx.test.ext:junit:$junit_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4"
    debugImplementation "androidx.compose.ui:ui-tooling"
    debugImplementation "androidx.compose.ui:ui-test-manifest"
}

kapt {
    correctErrorTypes true
}
